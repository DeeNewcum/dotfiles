#!/usr/bin/perl

# Produce bar graphs at the command line.

# A simplified version of:
#       http://search.cpan.org/dist/Text-Graph/Graph.pm
#       http://jehiah.cz/a/ascii-bar-charts

# Examples:
#       last | cut -d \  -f 1 | bargraph -c
#       last | perl -nle '/(\d\d):\d\d/ && print $1' | bargraph

# Arguments:
#       -c      sort by count, rather than sort by string

    use strict;
    use warnings;
    
    use List::Util qw[min max];

    use Data::Dumper;
    #use Devel::Comments;           # uncomment this during development to enable the ### debugging statements


my $sort_by_count = 0;
if (@ARGV && $ARGV[0] eq '-c') {
    shift @ARGV;
    $sort_by_count++;
}

my %count;

while (<>) {
    chomp;
    $count{$_}++;
}

my $min = min(values(%count));
my $max = max(values(%count));
my $longest_key_string = max(map {length} keys %count);
my $longest_value_string = max(map {length(sprintf "%d", $_)} values %count);

my $max_bar_len = 80;
my $divisor = ($max - $min + 1);

my @key_order = $sort_by_count ?
            sort {$count{$a} <=> $count{$b}} keys %count :
            sort {&natural_sort} keys %count;
foreach my $key (@key_order) {
    printf "%-${longest_key_string}s (%${longest_value_string}d)  %s\n",
                $key,
                $count{$key},
                '*' x (1 + ($max_bar_len * ($count{$key} - $min) / $divisor));
}


# Thanks to http://www.perlmonks.org/?node_id=68185
sub natural_sort {
  my @a = split /(\d+)/, $a;
  my @b = split /(\d+)/, $b;
  my $M = @a > @b ? @a : @b;
  my $res = 0;
  for (my $i = 0; $i < $M; $i++) {
    return -1 if ! defined $a[$i];
    return 1 if  ! defined $b[$i];
    if ($a[$i] =~ /\d/) {
      $res = $a[$i] <=> $b[$i];
    } else {
      $res = $a[$i] cmp $b[$i];
    }
    last if $res;
  }
  $res;
}
