#!/usr/bin/perl

# displays the path to the specified file, in the form that's useful to scp from another computer

    use strict;
    use warnings;

    use File::Spec;

@ARGV or die "Please specify a file\n";

############### hostname ################
my $host;
if (qx|uname| =~ /^SunOS$/) {
    ($host = qx[hostname]) =~ s/\s//g;
} else {
    ($host = qx[hostname --long]) =~ s/\s//g;
}
if (-e "$ENV{HOME}/.hostname_override") {               # 'hostname' doesn't always return the correct name/address for other computers to be able to contact it
        open FIN, "$ENV{HOME}/.hostname_override";
        ($host = do {local $/=undef;<FIN>}) =~ s/[\n\r]$//s;
        close FIN;
}

############### username ################
my $username = (getpwuid $<)[0];
if (-e "$ENV{HOME}/.username_override") {               # sometimes we want to override the username (if we 'sudo' a lot, for instance)
        open FIN, "$ENV{HOME}/.username_override";
        ($username = do {local $/=undef;<FIN>}) =~ s/[\n\r]$//s;
        close FIN;
}

foreach my $file (@ARGV) {
    -e $file            or warn "Specified file '$file' not found\n";

    my $abs = File::Spec->rel2abs($file);
    $abs .= "/" if -d $file;


    if (exists $ENV{CLEARCASE_ROOT} && $abs =~ m#^/vobs/#) {
        print "$username\@$host:$ENV{CLEARCASE_ROOT}$abs\n";
    } else {
        print "$username\@$host:$abs\n";
    }
}
